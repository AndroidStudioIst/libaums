apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'jacoco'

ext {
    artifact = 'libaums'
    bintrayName = 'libaums'
    libraryVersion = '0.8.0'
}

configurations {
    javadocDeps
}

android {
    compileSdkVersion 32
    buildToolsVersion '30.0.3'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 32
        versionCode 80
        versionName "0.8.0"

        externalNativeBuild {
            cmake {
                abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            testCoverageEnabled true
        }
    }

    lintOptions {
        abortOnError false
    }

    // TODO remove this?
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
        unitTests.returnDefaultValues = true
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }

        ndkVersion '23.1.7779620'
    }
}

dependencies {
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.apache.commons:commons-io:1.3.2'
    testImplementation 'com.eclipsesource.minimal-json:minimal-json:0.9.4'
    testImplementation 'org.xenei:junit-contracts:0.1.7'
    testImplementation 'org.mockito:mockito-core:2.28.2'

    api 'androidx.annotation:annotation:1.3.0'
    javadocDeps 'androidx.annotation:annotation:1.3.0'
    api 'androidx.core:core:1.8.0-alpha01'
    api "androidx.core:core-ktx:1.7.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

repositories {
    mavenCentral()
}

// https://about.codecov.io/blog/code-coverage-for-android-development-using-kotlin-jacoco-github-actions-and-codecov/
task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ]))
}
